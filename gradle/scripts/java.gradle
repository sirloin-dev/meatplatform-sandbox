/*
 * meatplatform-sandbox
 * Distributed under CC BY-NC-SA
 */
apply plugin: "java"

sourceCompatibility = version_target_jvm
targetCompatibility = version_target_jvm
[compileJava, compileTestJava]*.options*.encoding = "UTF-8"

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = "sources"
    from sourceSets.main.allSource
}

task testSourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = "test-sources"
    from sourceSets.test.allSource
}

artifacts {
    archives sourcesJar
    archives testSourcesJar
}

dependencies {
    // JSR 303: Bean validation
    implementation "javax.validation:validation-api:$version_javax_validation"

    // JSR 330: Dependency Injection for Java
    implementation "javax.inject:javax.inject:$version_javax_inject"

    // JSR 305: Annotations for Software Defect Detection
    implementation "com.google.code.findbugs:jsr305:$version_jsr305"
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:all" << "-Werror"
    }
}
